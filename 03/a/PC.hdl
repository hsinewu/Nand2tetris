// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    Register (in=feed, load=true, out=out, out=old);
    Inc16 (in=old, out=feedinc);
    Mux16 (a=old, b=feedinc, sel=inc, out=feed0);
    Mux16 (a=feed0, b=in, sel=load, out=feed1);
    Mux16 (a=feed1, b=false, sel=reset, out=feed);
}
